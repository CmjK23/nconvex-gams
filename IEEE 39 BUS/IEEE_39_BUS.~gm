$title Despacho termico IEEE BUS 39 CON EL METODO DE NIVEL (MULTICORTES)

option limcol=10;
option limrow=10;
option decimals=8;

SET
    i             "generador"   /1*10/
    NB            "nodo"        /B001*B039/
    ENL           "Linea"       /E001*E046/
    k             "Iteraciones" /1*50/
    Cut(k)        "Cortes generados"
;

alias (NB,j);

positive variable
prac(NB);
scalar
crac /100000000/;

*Parque Generador
*********************************************
* Termica
*********************************************
SET  GT  /
        T001 G1_InterconexionUSA_1000MW
        T002 G2_Slack_700MW
        T003 G3_Steam_800MW
        T004 G4_Steam_800MW
        T005 G5_Steam_300MW
        T006 G6_Steam_800MW
        T007 G7_Steam_700MW
        T008 G8_Steam_700MW
/;

SET  PtBus(GT,NB) /
        T001.B039
        T002.B031
        T003.B032
        T004.B033
        T005.B034
        T006.B035
        T007.B036
        T008.B037
/;

TABLE PtData(GT,*)
                Pgen      Pmin      Pmax    Costo1    CI1       CI2     Pmax1     Pmax2   Forzada   Sist  Calif
        T001 1000.000  800.000  1250.000  5000.00   20.00     20.00   1250.000  1250.000    no        1     101
        T002  520.810  100.000   700.000  7000.00   35.00     35.00    700.000   700.000    no        1     102
        T003  650.000  250.000   800.000  8000.00   40.00     40.00    800.000   800.000    no        1     103
        T004  632.000  250.000   800.000  8000.00   40.00     40.00    800.000   800.000    no        1     104
        T005  508.000  100.000   300.000  9000.00   45.00     45.00    300.000   300.000    no        1     105
        T006  650.000  250.000   800.000  8000.00   40.00     40.00    800.000   800.000    no        1     106
        T007  560.000  200.000   700.000  8000.00   40.00     40.00    700.000   700.000    no        1     107
        T008  540.000  200.000   700.000  8000.00   40.00     40.00    700.000   700.000    no        1     108
;
*********************************************
* Hidraulica
*********************************************
SET  GH  /
           H001 G9_Hydro_1000MW
           H002 G10_Hydro_1000MW
/;

SET  PhBus(GH,NB) /
        H001.B038
        H002.B030
/;

TABLE PhData(GH,*)
                Pgen      Pmin      Pmax      CI        Forzada   Sist    Cmgh
        H001   830.000  350.000  1000.000   2.00        no        1      yes
        H002   250.000  100.000  1000.000   2.00        no        1      yes
;

TABLE  FData(ENL,*)
        R0       X0        G0       Pmax    Cong  Sist
E001    0.000000  0.1305  0.0000   300.0    no     1
E002    0.000000  0.1305  0.0000   300.0    no     1
E003    0.000000  0.1750  0.0000   700.0    no     1
E004    0.000000  0.1600  0.0000   800.0    no     1
E005    0.000056  0.1136  0.0000   800.0    no     1
E006    0.000054  0.1080  0.0000   600.0    no     1
E007    0.000000  0.1144  0.0000   800.0    no     1
E008    0.000035  0.1904  0.0000   700.0    no     1
E009    0.000042  0.1624  0.0000   700.0    no     1
E010    0.000000  0.1810  0.0000  1000.0    no     1
E011    0.000080  0.1560  0.0000  1000.0    no     1
E012    0.000070  0.1380  0.0000  1000.0    no     1
E013    0.0035    0.0411  0.0000   300.0    no     1
E014    0.0010    0.0250  0.0000   300.0    no     1
E015    0.0013    0.0151  0.0000   300.0    no     1
E016    0.0070    0.0086  0.0000   300.0    no     1
E017    0.0013    0.0213  0.0000   300.0    no     1
E018    0.0011    0.0133  0.0000   300.0    no     1
E019    0.0008    0.0128  0.0000   300.0    no     1
E020    0.0008    0.0129  0.0000   300.0    no     1
E021    0.0002    0.0026  0.0000   300.0    no     1
E022    0.0008    0.0112  0.0000   300.0    no     1
E023    0.0006    0.0092  0.0000   300.0    no     1
E024    0.0007    0.0082  0.0000   300.0    no     1
E025    0.0004    0.0046  0.0000   300.0    no     1
E026    0.0023    0.0363  0.0000   300.0    no     1
E027    0.0010    0.0250  0.0000   300.0    no     1
E028    0.0004    0.0043  0.0000   300.0    no     1
E029    0.0004    0.0043  0.0000   300.0    no     1
E030    0.0009    0.0101  0.0000   300.0    no     1
E031    0.0018    0.0217  0.0000   300.0    no     1
E032    0.0009    0.0094  0.0000   300.0    no     1
E033    0.0007    0.0089  0.0000   300.0    no     1
E034    0.0016    0.0195  0.0000   300.0    no     1
E035    0.0008    0.0135  0.0000   300.0    no     1
E036    0.0003    0.0059  0.0000   300.0    no     1
E037    0.0007    0.0082  0.0000   300.0    no     1
E038    0.0013    0.0173  0.0000   300.0    no     1
E039    0.0008    0.0140  0.0000   300.0    no     1
E040    0.0006    0.0096  0.0000   300.0    no     1
E041    0.0022    0.0350  0.0000   300.0    no     1
E042    0.0032    0.0323  0.0000   300.0    no     1
E043    0.0014    0.0147  0.0000   300.0    no     1
E044    0.0043    0.0474  0.0000   300.0    no     1
E045    0.0057    0.0625  0.0000   300.0    no     1
E046    0.0014    0.0151  0.0000   300.0    no     1
;

*Conexion de la linea
SET Fbus(ENL,NB,NB)
/
        E001.B013.B012
        E002.B011.B012
        E003.B006.B031
        E004.B010.B032
        E005.B019.B033
        E006.B020.B034
        E007.B022.B035
        E008.B023.B036
        E009.B025.B037
        E010.B002.B030
        E011.B029.B038
        E012.B019.B020
        E013.B001.B002
        E014.B001.B039
        E015.B002.B003
        E016.B002.B025
        E017.B003.B004
        E018.B003.B018
        E019.B004.B005
        E020.B004.B014
        E021.B005.B006
        E022.B005.B008
        E023.B006.B007
        E024.B006.B011
        E025.B007.B008
        E026.B008.B009
        E027.B009.B039
        E028.B010.B011
        E029.B010.B013
        E030.B013.B014
        E031.B014.B015
        E032.B015.B016
        E033.B016.B017
        E034.B016.B019
        E035.B016.B021
        E036.B016.B024
        E037.B017.B018
        E038.B017.B027
        E039.B021.B022
        E040.B022.B023
        E041.B023.B024
        E042.B025.B026
        E043.B026.B027
        E044.B026.B028
        E045.B026.B029
        E046.B028.B029
  /;

PARAMETER b(NB,NB);

b(NB,j) = sum(ENL$Fbus(ENL,NB,j),
         -Fdata(ENL,'X0')/(FData(ENL,'R0')*FData(ENL,'R0')+
         FData(ENL,'X0')*FData(ENL,'X0')));

DISPLAY b;

TABLE demanda(NB,*)
                Pc
        B003  322.00
        B004  500.00
        B007  233.80
        B008  522.00
        B012  207.50
        B015  320.00
        B016  329.00
        B018  158.00
        B020  628.00
        B021  274.00
        B023  247.50
        B024  308.60
        B025  224.00
        B026  139.00
        B027  281.00
        B028  206.00
        B029  283.50
        B031  209.20
        B039 1104.00
DISPLAY demanda;
PARAMETER Loss(ENL);
Loss(ENL)=0;

*==============================================================================*
*--------------------PARAMETROS DE CONTROL DEL ALGORITMO-----------------------
*==============================================================================*
SCALAR
       alpha     "Parametro de nivel (step size parameter)(sensibilidad)" /0.2/
       Nivel     "Nivel objetivo para la proyeccion"
       LB        "Lower Bound (Mejor dual encontrado)" /-1e9/
       UB        "Upper Bound (Estimado por el maestro)" /1e9/
       Terminado "Bandera de parada" /0/
       Gap       "Brecha de convergencia" /0.1/
       epsilon   "Tolerancia de convergencia" /0.001/
       L_actual  "Valor real de la funcion dual";

PARAMETER
          pi_iter(NB)              "Precio candidato actual (pi_k)"
          pi_stab(NB)              "Centro de estabilidad (pi_stab)"
          pi_lo(NB)                "Limite inferior precios"
          pi_up(NB)                "Limite superior precios"
          hist_pi(NB, k)           "Precio nodal usado en la iteracion k"
          hist_profit_GT(GT, k)    "Beneficio termico en iteracion k"
          hist_p_GT(GT, k)         "Potencia termica en iteracion k"
          hist_profit_GH(GH, k)    "Beneficio hidro en iteracion k"
          hist_p_GH(GH, k)         "Potencia hidro en iteracion k";

* Almacenar evolucion del LB= Lower Bound, UB= Upper Bound y el gap *
PARAMETER hist_LB(k)  "Historial de Lower Bound"
          hist_UB(k)  "Historial de Upper Bound"
          hist_Gap(k) "Historial de Gap";

pi_iter(NB) = 30;
pi_stab(NB) = 30;
pi_lo(NB) = 0;
pi_up(NB) = 9000;

*inicializar cortes en vacio*
Cut(k) = no;

*==============================================================================*
*---------------------------------Generacion-----------------------------------
*==============================================================================*
VARIABLES
          z_slave_GT           "Beneficio de los Generadores Termico (Ecuacion 2a) "
          z_slave_GH           "Beneficio del Generadores Hidraulico (Ecuacion 2a) "
          z_slave_GT_ind(GT)   "Beneficio individual de cada GT"
          z_slave_GH_ind(GH)   "Beneficio individual de cada GH"

          p_GT(GT)             "Potencia Termica"
          u_GT(GT)             "Binaria de compromiso Termico"
          p_GH(GH)             "Potencia Hidraulica Total"

;

BINARY VARIABLE u_GT;
POSITIVE VARIABLE p_GT, p_GH;

EQUATIONS
         eq_obj_GT_total    "Objetivo total GT como suma de individuales"
         eq_obj_GH_total    "Objetivo total GH como suma de individuales"
         eq_obj_GT(GT)      "Max Beneficio Termico (Ecuacion 4b )"
         eq_min_GT(GT)      "Limite potencia termo (Pmin)"
         eq_max_GT(GT)      "Limite potencia termo (Pmax)"
         eq_obj_GH(GH)      "Max Beneficio Hidro (Ecuacion 4b)"
         eq_max_GH(GH)      "Limites potencia hidro (Pmax)"
         eq_min_GH(GH)      "Limites potencia hidro (Pmin)"
;

eq_obj_GT_total.. z_slave_GT =e= sum(GT, z_slave_GT_ind(GT));
eq_obj_GH_total.. z_slave_GH =e= sum(GH, z_slave_GH_ind(GH));

eq_obj_GT(GT)..
         z_slave_GT_ind(GT) =e=
          ( sum(NB$PtBus(GT,NB), pi_iter(NB)) * p_GT(GT) ) - (PtData(GT,'Costo1') * u_GT(GT)
          + PtData(GT,'CI1')*(p_GT(GT) - PtData(GT,'Pmin')*u_GT(GT))
          );

eq_min_GT(GT)..  p_GT(GT) =g= PtData(GT,'Pmin');

eq_max_GT(GT)..  p_GT(GT) =l= PtData(GT,'Pmax');

eq_obj_GH(GH)..
    z_slave_GH_ind(GH) =e= sum(NB$PhBus(GH,NB), pi_iter(NB)) * p_GH(GH)
                     - ( PhData(GH,'CI') * p_GH(GH) );
eq_max_GH(GH)..
    p_GH(GH) =l= PhData(GH,'Pmax');
eq_min_GH(GH)..
    P_GH(GH) =g= PhData(GH,'Pmin');
*==============================================================================*
*---------------------------------RED -----------------------------------
*==============================================================================*
VARIABLE
         z_slave_net        "Renta de Congestion Total de la Red"
         f_dc_linea(ENL)    "Flujo DC en linea"
         ang(NB)            "Angulo de voltaje"
         inj_fromto(NB)     "Inyeccion neta (Gen - Carga) vista por la red";

EQUATIONS
         eq_obj_net         " Congestion (Ecuacion 4c)"
         eq_flujo_dc(ENL)   "Ley de Kirchhoff DC (Ecuacion 10d)"
         eq_bal_net(NB)     "Balance Nodal Red"
;

eq_obj_net..    z_slave_net =e= sum(NB, pi_iter(NB) * inj_fromto(NB));

eq_flujo_dc(ENL)..  f_dc_linea(ENL) =e= sum((NB,j)$Fbus(ENL,NB,j), (ang(NB) - ang(j))/FData(ENL,'X0'));

eq_bal_net(NB)..   inj_fromto(NB) =e= sum((ENL,j)$Fbus(ENL,NB,j), f_dc_linea(ENL)) - sum((ENL,j)$Fbus(ENL,j,NB), f_dc_linea(ENL));

f_dc_linea.up(ENL) =  FData(ENL,'Pmax');
f_dc_linea.lo(ENL) = -FData(ENL,'Pmax');

*Nodo de referencia*
ang.fx('B001') = 0;
ang.lo(NB)=-PI;
ang.up(NB)=PI;

Model Slave_GT  /eq_obj_GT_total, eq_obj_GT,eq_min_GT,eq_max_GT/;
Model Slave_GH  /eq_obj_GH_total, eq_obj_GH, eq_min_GH/;
Model Slave_Net /eq_obj_net, eq_flujo_dc, eq_bal_net/;

Slave_GT.optcr = 0;

* ==============================================================================
*                        PROBLEMA MAESTRO Y PROYECCIÓN
* ==============================================================================


VARIABLES
            Z_dual             "Valor aproximado de la funcion dual (L_hat)"
            theta_GT(GT)       "Variable auxiliar: Beneficio aproximado Termico"
            theta_GH(GH)       "Variable auxiliar: Beneficio aproximado Hidro"
            theta_net          "Variable auxiliar: Renta de Congestion aproximada"
            pi_new(NB)         "Nuevo precio candidato (Variable de decision)"
            dist               "Distancia cuadratica al centro de estabilidad"
;

EQUATIONS
            eq_obj_master      "Maximizar funcion dual aproximada (Ecuacion 6 / 12a)"
            eq_cut_GT(GT, k)   "Multicortes para Termicos (Ecuacion 12b)"
            eq_cut_GH(GH, k)   "Multicortes para Hidros (Ecuacion 12b)"
            eq_cut_net(k)      "Cortes para la Red (Ecuacion 12d)"
            eq_min_dist        "Minimizar distancia (Ecuacion 9 Objetivo)"
            eq_nivel_rst       "Restriccion de Nivel (Ecuacion 9 Restriccion)"
;

eq_obj_master..    Z_dual =e= sum(NB, pi_new(NB) * demanda(NB,'Pc'))
             - sum(GT, theta_GT(GT))
             - sum(GH, theta_GH(GH))
             - theta_net;

eq_cut_GT(GT, Cut)..    theta_GT(GT) =g= hist_profit_GT(GT, Cut)
                   + sum(NB$PtBus(GT,NB),
                         hist_p_GT(GT, Cut) * (pi_new(NB) - hist_pi(NB, Cut))
                     );

eq_cut_GH(GH, Cut)..    theta_GH(GH) =g= hist_profit_GH(GH, Cut)
                   + sum(NB$PhBus(GH,NB),
                         hist_p_GH(GH, Cut) * (pi_new(NB) - hist_pi(NB, Cut))
                     );

eq_cut_net(Cut)..
    theta_net =g= 0;

eq_min_dist..
    dist =e= sum(NB, sqr(pi_new(NB) - pi_stab(NB)));

eq_nivel_rst..
    Z_dual =g= Nivel;

Model Maestro    /eq_obj_master, eq_cut_GT, eq_cut_GH, eq_cut_net/;
Model Proyeccion /eq_obj_master, eq_cut_GT, eq_cut_GH, eq_cut_net, eq_min_dist, eq_nivel_rst/;

Maestro.optcr = 0;
Proyeccion.optcr = 0;

pi_new.lo(NB) = 0;
pi_new.up(NB) = 2000;
Scalar k_count "Numero total de iteraciones" /0/;

Loop(k$(ord(k) <= 50 and Terminado = 0),

    k_count=k_count+1;

    Solve Slave_GT using mip maximizing z_slave_GT;

    Solve Slave_GH using lp maximizing z_slave_GH;

    Solve Slave_Net using lp minimizing z_slave_net;

    Cut(k) = yes;

    hist_pi(NB, k) = pi_iter(NB);

    hist_profit_GT(GT, k) = z_slave_GT_ind.l(GT);
    hist_p_GT(GT, k) = p_GT.l(GT);

    hist_profit_GH(GH, k) = z_slave_GH_ind.l(GH);
    hist_p_GH(GH, k) = p_GH.l(GH);

    L_actual = sum(NB, pi_iter(NB) * demanda(NB,'Pc'))
             - sum(GT, z_slave_GT_ind.l(GT))
             - sum(GH, z_slave_GH_ind.l(GH))
             - z_slave_net.l;

    if(L_actual > LB,
        LB = L_actual;
        pi_stab(NB) = pi_iter(NB);
    );
    Display "Iteracion:", k_count, "LB:", LB, "UB:", UB, "Gap:", gap;
    Solve Maestro using lp maximizing Z_dual;

    UB = Z_dual.l;

    Gap = (UB - LB) / (abs(LB));
    hist_LB(k) = LB;
    hist_UB(k) = UB;
    hist_Gap(k) = Gap;

    if(Gap < epsilon,
        Terminado = 1;
    );

    if(Terminado = 0,
        Nivel = alpha * UB + (1 - alpha) * LB;
        Solve Proyeccion using qcp minimizing dist;
        pi_iter(NB) = pi_new.l(NB);
    );

);

Display "Numero de iteraciones", k_count;
Display "Historial del gap ", hist_Gap;
Display "Historial del Lowwer Bound ", hist_LB;
Display "Historial del Upper  Bound ", hist_UB;
Display hist_profit_GT,hist_p_GT,hist_profit_GH,hist_p_GH;

